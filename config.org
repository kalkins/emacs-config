#+TITLE: Emacs configuration
#+AUTHOR: Sindre Stephansen
#+EMAIL: sindre@sindrestephansen.com
#+LANGUAGE: en
#+LINK_HOME: http://sindrestephansen.com
#+STARTUP: indent overview

This is my configuration file for emacs (exported as twitter bootstrap html),
the source of which can be found [[http://github.com/kalkins/emacs-config][here]]. The config is written using org mode,
and all code blocks are executed at launch.

* Startup
** Toolbars and scollbar
Hide the toolbar, menubar and scrollbar.

#+BEGIN_SRC emacs-lisp
(tool-bar-mode -1)
(menu-bar-mode -1)
(scroll-bar-mode -1)
#+END_SRC

** Theme
#+BEGIN_SRC emacs-lisp
(load-theme 'wombat)
#+END_SRC

* Package management
** Package.el
#+BEGIN_SRC emacs-lisp
(require 'package)
#+END_SRC

Add extra package providers.

#+BEGIN_SRC emacs-lisp
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
(add-to-list 'package-archives '("melpa-stable" . "https://stable.melpa.org/packages/"))
(add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/"))
#+END_SRC

Initialize Package.el, and make sure use-package is installed

#+BEGIN_SRC emacs-lisp
(package-initialize)
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))
(require 'use-package)
#+END_SRC

** Use-package
Use-package allows for easier handling of packages, like setting settings and keybindings.

Automatically download required packages.

#+BEGIN_SRC emacs-lisp
(setq use-package-always-ensure t)
#+END_SRC

** Package update

#+BEGIN_SRC emacs-lisp
(use-package auto-package-update
  :config
  (setq auto-package-update-interval 7
        auto-package-update-prompt-before-update t
        auto-package-update-delete-old-versions t)
  (auto-package-update-maybe))
#+END_SRC
* Display
** Powerline
#+BEGIN_SRC emacs-lisp
(use-package powerline
  :config
  (powerline-center-evil-theme))
#+END_SRC

** Font
*** Fira code
#+BEGIN_SRC emacs-lisp
(use-package fira-code-mode
  :hook prog-mode)
#+END_SRC

*** Increase and decrease font size
#+BEGIN_SRC emacs-lisp
; Source: https://coderwall.com/p/ifgyag/change-font-size-in-emacs-dynamically-based-on-screen-resolution
(setq default-font "Fira code-%d")
(setq font-size-low-res 10)
(setq font-size-high-res 10)

(defun frame-is-high-res ()
  (> (display-pixel-width) 10000))

(defun fontify-frame (&optional frame)
  (interactive)
  (if window-system
      (let ((font (format
		   default-font
		   (if (frame-is-high-res)
		       font-size-high-res
		     font-size-low-res))))

	(set-frame-parameter frame 'font font))))

(defun frame-increase-font-size (&optional frame)
  (interactive)
  (progn
    (if (frame-is-high-res)
	(cl-incf font-size-high-res)
      (cl-incf font-size-low-res))
    (fontify-frame frame)))

(defun frame-decrease-font-size (&optional frame)
  (interactive)
  (progn
    (if (frame-is-high-res)
	(cl-decf font-size-high-res)
      (cl-decf font-size-low-res))
    (fontify-frame frame)))

(fontify-frame nil)

(global-set-key (kbd "C-M-j") 'frame-decrease-font-size)
(global-set-key (kbd "C-M-ø") 'frame-increase-font-size)

(push 'fontify-frame after-make-frame-functions)
#+END_SRC

* Alarm bell
Emacs sometimes makes an annoying bell sound. This replaces that with
a visible cue.

#+BEGIN_SRC emacs-lisp
(setq ring-bell-function
      (lambda ()
	(let ((orig-fg (face-foreground 'mode-line)))
	  (set-face-foreground 'mode-line "#F2804F")
	  (run-with-idle-timer 0.1 nil
			       (lambda (fg) (set-face-foreground 'mode-line fg))
			       orig-fg))))
#+END_SRC

* Search
** Helm
#+BEGIN_SRC emacs-lisp
(use-package helm
  :bind (("M-x" . helm-M-x)
	 ("C-x r b" . helm-filtered-bookmarks)
	 ("C-x C-f" . helm-find-files)
	 ("C-f" . helm-find))
  :config
  (helm-mode 1))
#+END_SRC
* Evil mode
** Install
This package adds vim-like navigation to emacs.

#+BEGIN_SRC emacs-lisp
(use-package evil
  :init
  (setq evil-want-keybinding nil)
  :config
  (evil-mode 1))
#+END_SRC

** Keybinding
I prefer to use jklø for navigation instead of hjkl.

#+BEGIN_SRC emacs-lisp
(evil-global-set-key 'motion (kbd "j") 'evil-backward-char)
(evil-global-set-key 'motion (kbd "k") 'evil-next-line)
(evil-global-set-key 'motion (kbd "l") 'evil-previous-line)
(evil-global-set-key 'motion (kbd "ø") 'evil-forward-char)
					; For some reason ø is M-c in sudoedit/terminal mode
					; TODO: This works, but sets a C-u count to 8, meaning that
					; the next command is executed 8 times
(evil-global-set-key 'motion (kbd "M-c") 'evil-forward-char)
#+END_SRC

I like to insert blank newlines with RET in normal mode.

#+BEGIN_SRC emacs-lisp
(define-key evil-normal-state-map (kbd "RET") (lambda ()
						(interactive)
						(next-line)
						(beginning-of-line)
						(newline)
						(previous-line)))
#+END_SRC

Don't remap undo buttons in visual mode.

#+BEGIN_SRC emacs-lisp
(define-key evil-visual-state-map (kbd "u") 'undo-tree-undo)
(define-key evil-visual-state-map (kbd "C-r") 'undo-tree-redo)
#+END_SRC

Disable some keys, so they can be used for other stuff later.

#+BEGIN_SRC emacs-lisp
(define-key evil-motion-state-map (kbd "C-f") nil)
#+END_SRC

** Evil collection
The evil collection adds bindings for several modes not covered by evil.

#+BEGIN_SRC emacs-lisp
(defun jklø-evil-collection (_mode mode-keymaps &rest _rest)
  (evil-collection-translate-key 'motion mode-keymaps
    "k" "j"
    "l" "k"
    "ø" "l"))

(use-package evil-collection
  :after evil
  :config
  (add-hook 'evil-collection-setup-hook #'jklø-evil-collection)
  (evil-collection-init))
#+END_SRC

* PDF tools
Use PDF tools instead of the default DocView.

#+BEGIN_SRC emacs-lisp
(defun pdf-view-goto-page-or-first (count)
  "Goto page COUNT.
  If COUNT is not supplied, go to the first page."
  (interactive "P")
  (if count
      (pdf-view-goto-page count)
    (pdf-view-first-page)))

(defun pdf-view-goto-page-or-last (count)
  "Goto page COUNT.
  If COUNT is not supplied, go to the last page."
  (interactive "P")
  (if count
      (pdf-view-goto-page count)
    (pdf-view-last-page)))

(use-package pdf-tools
  :mode "\\.pdf\\'"
  :interpreter "pdf"
  :bind (:map pdf-view-mode-map
	      ("j"   . pdf-view-previous-page-command)
	      ("k"   . (lambda () (interactive) (pdf-view-next-line-or-next-page 5)))
	      ("l"   . (lambda () (interactive) (pdf-view-previous-line-or-previous-page 5)))
	      ("ø"   . pdf-view-next-page-command)
	      ("g"   . pdf-view-goto-page-or-first)
	      ("G"   . pdf-view-goto-page-or-last)
	      ("C-o" . pdf-history-backward)
	      ("C-i" . pdf-history-forward)
	      ("m"   . pdf-view-position-to-register)
	      ("'"   . pdf-view-jump-to-register)
	      ("/"   . pdf-occur)
	      ("o"   . pdf-outline)
	      ("f"   . pdf-links-action-perform)
	      ("b"   . pdf-view-midnight-minor-mode)
	      ("l"   . pdf-view-next-page-command))
  :config
  (pdf-tools-install))
#+END_SRC

* Git
** Smerge
#+BEGIN_SRC emacs-lisp
(setq smerge-command-prefix (kbd "C-c m"))
#+END_SRC

** Magit
#+BEGIN_SRC emacs-lisp
(use-package magit
  :bind ("M-m" . magit-status))
#+END_SRC

** Magit-todos
#+BEGIN_SRC emacs-lisp
(use-package magit-todos
  :config
  (magit-todos-mode))
#+END_SRC

* Projectile
I use projectile for switching between and navigating projects

#+BEGIN_SRC emacs-lisp
(use-package projectile
  :config
  (setq projectile-globally-ignored-files
	(append '("~"
		  ".swp"
		  ".pyc")
		projectile-globally-ignored-files))
  (projectile-mode)
  (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map))

(use-package helm-projectile
  :config
  (helm-projectile-on))
#+END_SRC

* Line numbers
I have line numbers off by default, but I want to
be able to quickly turn them of and on. I often
need relative numbers for evil mode commands.

This setup toggles line numbers with 'h', and
relative numbers with 'æ'. If I hit 'æ' while
line numbers are toggled, it switched to
relative numbers. If I hit 'æ' again it turns
all line numbers off.

#+BEGIN_SRC emacs-lisp
(use-package linum-relative
  :config
  (setq linum-relative-mode nil)
  (define-key evil-normal-state-map (kbd "h") (lambda ()
						(interactive)
						(if (and linum-mode (not linum-relative-mode))
						    (linum-mode -1)
						  (progn
						    (linum-relative-off)
						    (linum-mode 1)
						    (setq linum-relative-mode nil)))))
  (define-key evil-normal-state-map (kbd "æ") (lambda ()
						(interactive)
						(if (and linum-mode linum-relative-mode)
						    (progn
						      (linum-relative-off)
						      (linum-mode -1)
						      (setq linum-relative-mode nil))
						  (progn
						    (linum-mode 1)
						    (linum-relative-on)
						    (setq linum-relative-mode t))))))
#+END_SRC

* Autosaves and backups
I prefer putting all autosaves and backups in one directory,
so they don't clutter up my projects.

#+BEGIN_SRC emacs-lisp
(defconst emacs-saves-dir "~/tmp/emacs")
(setq backup-directory-alist `((".*" . ,emacs-saves-dir))
      auto-save-file-name-transforms `((".*" ,emacs-saves-dir t))
      backup-by-copying t
      delete-old-versions t
      kept-new-versions 6
      kept-old-versions 2
      version-control t
      make-backup-files nil)
#+END_SRC

* Flycheck
#+BEGIN_SRC emacs-lisp
(use-package flycheck
  :config
  (global-flycheck-mode))

(use-package flycheck-inline
  :config
  (add-hook 'flycheck-mode-hook #'flycheck-inline-mode))
#+END_SRC

* Autocomplete
#+BEGIN_SRC emacs-lisp
(use-package company
  :config
  (add-hook 'after-init-hook 'global-company-mode)
  (setq company-dabbrev-downcase 0)
  (setq company-dabbrev-other-buffers t)
  (setq company-idle-delay 0.5)
  (let ((bg (face-attribute 'default :background)))
    (custom-set-faces
     '(company-tooltip ((t (:background "#cccccc" :foreground "black"))))
     '(company-scrollbar-bg ((t (:background "#999999"))))
     '(company-scrollbar-fg ((t (:background "#555555")))))))

					;(use-package auto-complete
					;  :init
					;  (global-auto-complete-mode))
#+END_SRC

* Indentation

#+BEGIN_SRC emacs-lisp
(use-package smart-tabs-mode
  :config
  (smart-tabs-insinuate 'c))

(setq-default indent-tabs-mode nil)
#+END_SRC

* Programming languages
** Web development
*** HTML
**** Indentation

Set HTML indentation to 4 spaces by default.

#+BEGIN_SRC emacs-lisp
(add-hook 'html-mode-hook
	  (lambda ()
	    (set (make-local-variable 'sgml-basic-offset) 4)))
#+END_SRC
*** PHP

#+BEGIN_SRC emacs-lisp
(use-package php-mode)
#+END_SRC

** Python
*** Virtualenv
#+BEGIN_SRC emacs-lisp
(use-package virtualenvwrapper
  :config
  (venv-initialize-interactive-shells)
  (setq venv-location "~/env/"))

(use-package auto-virtualenvwrapper
  :init
  (add-hook 'python-mode-hook #'auto-virtualenvwrapper-activate))
#+END_SRC

*** Autocomplete
#+BEGIN_SRC emacs-lisp
(defun jedi-python-mode-hook ()
  (add-to-list 'company-backends 'company-jedi))

(use-package company-jedi
  :init
  (add-hook 'python-mode-hook 'jedi-python-mode-hook))

					;(use-package company-anaconda
					;  :init
					;  (add-to-list 'company-backends 'company-anaconda)
					;  (add-hook 'python-mode-hook 'anaconda-mode))

					;(require 'django-html-mode)
					;(use-package django-mode)
#+END_SRC

*** Shell
#+BEGIN_SRC emacs-lisp
(setq python-shell-interpreter "ipython"
      python-shell-interpreter-args "--simple-prompt -i")
#+END_SRC
** C/C++

#+BEGIN_SRC emacs-lisp
(use-package irony
  :config
  (add-hook 'c++-mode-hook 'irony-mode)
  (add-hook 'c-mode-hook 'irony-mode)
  (add-hook 'irony-mode-hook 'irony-cdb-autosetup-compile-options))

(use-package flycheck-irony
  :config
  (add-hook 'flycheck-mode-hook #'flycheck-irony-setup))

(use-package company-irony-c-headers
  :config
  (eval-after-load 'company
    '(add-to-list 'company-backends 'company-irony-c-headers)))

(use-package company-irony
  :config
  (eval-after-load 'company
    '(add-to-list 'company-backends 'company-irony)))

(use-package cmake-mode)

(setq-default c-default-style "linux"
	      c-basic-offset 4)

(add-hook 'c-mode-common-hook
	  (lambda () (setq indent-tabs-mode t)))
#+END_SRC

** ASM

#+BEGIN_SRC emacs-lisp
(evil-define-key nil asm-mode-map (kbd "C-j") nil)
#+END_SRC

** Rust

#+BEGIN_SRC emacs-lisp
(use-package rust-mode)

(use-package flycheck-rust
  :config
  (add-hook 'flycheck-mode-hook #'flycheck-rust-setup))
#+END_SRC
** Javascript
#+BEGIN_SRC emacs-lisp
(defun setup-tide-mode ()
  (interactive)
  (tide-setup)
  (flycheck-mode +1)
  (setq flycheck-check-syntax-automatically '(save mode-enabled))
  (eldoc-mode +1)
  (tide-hl-identifier-mode +1)
  (company-mode +1))

(use-package tide
  :config
  (setq company-tooltip-align-annotations t)
  (add-hook 'js-mode-hook #'setup-tide-mode))
#+END_SRC
** Latex
*** AucTex
#+BEGIN_SRC emacs-lisp
(use-package tex
  :ensure auctex
  :config
  (setq TeX-auto-save t)
  (setq TeX-parse-self t)
  (setq-default TeX-master nil))

(use-package company-auctex)
#+END_SRC

*** Configure texmathp
Texmathp is a tool that can tell if the cursor is in a math environment or not
However, its definition of math environment is quite small, so I add a few here.

#+BEGIN_SRC emacs-lisp
(setq texmathp-tex-commands '(("equation*" env-on)
                              ("align" env-on)
                              ("align*" env-on)))
#+END_SRC

*** Biblatex
Configure bibtex mode to use biblatex.

#+BEGIN_SRC emacs-lisp
(setq bibtex-dialect 'biblatex)
#+END_SRC

* HideShow
Collapses code blocks.

#+BEGIN_SRC emacs-lisp
(defun toggle-selective-display (column)
  (interactive "P")
  (set-selective-display
   (or column
       (unless selective-display
	 (1+ (current-column))))))

(defun toggle-hiding (column)
  (interactive "P")
  (if hs-minor-mode
      (if (condition-case nil
	      (hs-toggle-hiding)
	    (error t))
	  (hs-show-all))
    (toggle-selective-display column)))

(load-library "hideshow")
(define-key evil-motion-state-map (kbd "C-M-i") 'toggle-hiding)

(add-hook 'c-mode-common-hook 'hs-minor-mode)
(add-hook 'emacs-lisp-mode-hook 'hs-minor-mode)
(add-hook 'lisp-mode-hook 'hs-minor-mode)
(add-hook 'java-mode-hook 'hs-minor-mode)
(add-hook 'sh-mode-hook 'hs-minor-mode)
(add-hook 'html-mode-hook 'hs-minor-mode)
#+END_SRC

* Yasnippet
#+BEGIN_SRC emacs-lisp
(use-package yasnippet
  :bind (("C-c y c" . yas-new-snippet)
	 ("C-c y i" . yas-insert-snippet))
  :init
  (setq yas-snippet-dirs
	(list (concat
	       (file-name-directory (or load-file-name buffer-file-name))
	       "snippets")))
  (yas-global-mode 1))
#+END_SRC

* Org Mode
** Install
#+BEGIN_SRC emacs-lisp
(use-package org
  :ensure org-plus-contrib
  :bind (:map org-mode-map
         ("RET" . (lambda () (interactive) (org-return t)))
         ("C-c l" . org-store-link))
  :config
  (evil-define-key 'normal org-mode-map (kbd "<tab>") #'org-cycle))
  ;(unbind-key "C-j" org-mode-map)
  ;(unbind-key "C-k" org-mode-map))
#+END_SRC

** Bullets
Use pretty bullets instead of asterisks in headings.

#+BEGIN_SRC emacs-lisp
(use-package org-bullets
  :after org
  :config
  (add-hook 'org-mode-hook
	    (lambda () (org-bullets-mode t))))
#+END_SRC

Only use one indented bullet per heading.

#+BEGIN_SRC emacs-lisp
;(setq org-hide-leading-stars t)
#+END_SRC

** Ignore headlines
This allows headlines to be ignored, while including their content, by
marking them with the :ignore: tag.

#+BEGIN_SRC emacs-lisp
;(ox-extras-activate '(ignore-headlines))
#+END_SRC

** Grammar check
#+BEGIN_SRC emacs-lisp
(require 'langtool)
(use-package langtool
  :init
  (setq langtool-java-classpath "/usr/share/languagetool:/usr/share/java/languagetool/*"))
#+END_SRC

** Indent mode
Use indent-mode by default (indent text to match bullets)

#+BEGIN_SRC emacs-lisp
(add-hook 'org-mode-hook 'org-indent-mode)
#+END_SRC

** Source code blocks
Use proper syntax highlighting in these source blocks.

#+BEGIN_SRC emacs-lisp
(setq org-src-fontify-natively t)
#+END_SRC

Org mode likes to indent the code in the block two spaces (so it lines up with BEGIN_SRC),
which breaks the code alignment when using tabs.

#+BEGIN_SRC emacs-lisp
(setq org-edit-src-content-indentation 0)
(setq org-src-tab-acts-natively t)
(setq org-src-preserve-indentation t)
#+END_SRC

Keybinding for editing src blocks.

#+BEGIN_SRC emacs-lisp
(define-key org-mode-map (kbd "C-c e") 'org-edit-src-code)
#+END_SRC

** Latex
*** Biblatex
To properly include bibliography processesing, the latex command has to be modified.

Note that this requires biber to be installed.

#+BEGIN_SRC emacs-lisp
(setq org-latex-pdf-process
  '("latexmk -pdflatex='pdflatex -shell-escape -interaction nonstopmode -output-directory %o' -pdf -bibtex -f %f"))
#+END_SRC

*** Helper modes
This mode works together with AUCtex to make latex entry easier.

#+BEGIN_SRC emacs-lisp
(use-package cdlatex)
#+END_SRC

*** Preview size
Make inline latex previews bigger, so it's easier to read.

#+BEGIN_SRC emacs-lisp
(plist-put org-format-latex-options :scale 1.5)
#+END_SRC
*** Syntax highlight
#+BEGIN_SRC emacs-lisp
(require 'ox-latex)
(add-to-list 'org-latex-packages-alist '("" "minted"))
(setq org-latex-listings 'minted)
(setq org-src-fontify-natively t)
#+END_SRC
** References
#+BEGIN_SRC emacs-lisp
(use-package org-ref
  :config
  (setq org-ref-bibliography-notes "~/Nextcloud/Dokumenter/bibliography/notes.org"
        org-ref-default-bibliography '("~/Nextcloud/Dokumenter/bibliography/references.bib")
        org-ref-pdf-directory "~/Nextcloud/Dokumenter/bibliography/bibtex-pdfs"))
#+END_SRC

** Inline youtube links
A copy of [[https://github.com/TobiasZawada/org-yt/blob/master/org-yt.el][this package], since it's not available on MELPA.

#+BEGIN_SRC emacs-lisp
  ;;; org-yt.el --- Org youtube links.                 -*- lexical-binding: t; -*-

;; Copyright (C) 2018  U-ESI-INTERNAL\TOZ

;; Author: U-ESI-INTERNAL\TOZ <TOZ@smtp.1und1.de>
;; Keywords: multimedia

;; This program is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

  ;;; Commentary:

;; Idea from  https://emacs.stackexchange.com/questions/38098/org-mode-custom-youtube-link-syntax

  ;;; Code:

(require 'org)
(require 'org-element)

(defcustom org-yt-url-protocol "yt"
  "Protocol identifier for youtube links."
  :group 'org-yt
  :type 'string)

(defun org-image-update-overlay (file link &optional data-p refresh)
  "Create image overlay for FILE associtated with org-element LINK.
  If DATA-P is non-nil FILE is not a file name but a string with the image data.
  If REFRESH is non-nil don't download the file but refresh the image.
  See also `create-image'.
  This function is almost a duplicate of a part of `org-display-inline-images'."
  (when (or data-p (file-exists-p file))
    (let ((width
	   ;; Apply `org-image-actual-width' specifications.
	   (cond
	    ((not (image-type-available-p 'imagemagick)) nil)
	    ((eq org-image-actual-width t) nil)
	    ((listp org-image-actual-width)
	     (or
	      ;; First try to find a width among
	      ;; attributes associated to the paragraph
	      ;; containing link.
	      (let ((paragraph
		     (let ((e link))
		       (while (and (setq e (org-element-property
					    :parent e))
				   (not (eq (org-element-type e)
					    'paragraph))))
		       e)))
		(when paragraph
		  (save-excursion
		    (goto-char (org-element-property :begin paragraph))
		    (when
			(re-search-forward
			 "^[ \t]*#\\+attr_.*?: +.*?:width +\\(\\S-+\\)"
			 (org-element-property
			  :post-affiliated paragraph)
			 t)
		      (string-to-number (match-string 1))))))
	      ;; Otherwise, fall-back to provided number.
	      (car org-image-actual-width)))
	    ((numberp org-image-actual-width)
	     org-image-actual-width)))
	  (old (get-char-property-and-overlay
		(org-element-property :begin link)
		'org-image-overlay)))
      (if (and (car-safe old) refresh)
	  (image-refresh (overlay-get (cdr old) 'display))
	(let ((image (create-image file
				   (and width 'imagemagick)
				   data-p
				   :width width)))
	  (when image
	    (let* ((link
		    ;; If inline image is the description
		    ;; of another link, be sure to
		    ;; consider the latter as the one to
		    ;; apply the overlay on.
		    (let ((parent
			   (org-element-property :parent link)))
		      (if (eq (org-element-type parent) 'link)
			  parent
			link)))
		   (ov (make-overlay
			(org-element-property :begin link)
			(progn
			  (goto-char
			   (org-element-property :end link))
			  (skip-chars-backward " \t")
			  (point)))))
	      (overlay-put ov 'display image)
	      (overlay-put ov 'face 'default)
	      (overlay-put ov 'org-image-overlay t)
	      (overlay-put
	       ov 'modification-hooks
	       (list 'org-display-inline-remove-overlay))
	      (push ov org-inline-image-overlays)
	      ov)))))))

(defun org-yt-get-image (url)
  "Retrieve image from URL."
  (let ((image-buf (url-retrieve-synchronously url)))
    (when image-buf
      (with-current-buffer image-buf
	(goto-char (point-min))
	(when (looking-at "HTTP/")
	  (delete-region (point-min)
			 (progn (re-search-forward "\n[\n]+")
				(point))))
	(buffer-substring-no-properties (point-min) (point-max))))))

(defconst org-yt-video-id-regexp "[-_[:alnum:]]\\{10\\}[AEIMQUYcgkosw048]"
  "Regexp matching youtube video id's taken from `https://webapps.stackexchange.com/questions/54443/format-for-id-of-youtube-video'.")

(defun org-yt-follow (video-id)
  "Open youtube with VIDEO-ID."
  (browse-url (concat "https://youtu.be/" video-id)))

(defun org-yt-image-data-fun (_protocol link _description)
  "Get image corresponding to LINK from youtube.
  Use this as :image-data-fun property in `org-link-properties'.
  See `org-display-user-inline-images' for a description of :image-data-fun."
  (when (string-match org-yt-video-id-regexp link)
    (org-yt-get-image (format "http://img.youtube.com/vi/%s/0.jpg" link))))

(org-link-set-parameters org-yt-url-protocol
			 :follow #'org-yt-follow
			 :image-data-fun #'org-yt-image-data-fun)

(require 'subr-x)

(defun org-display-user-inline-images (&optional _include-linked _refresh beg end)
  "Like `org-display-inline-images' but for image data links.
  _INCLUDE-LINKED and _REFRESH are ignored.
  Restrict to region between BEG and END if both are non-nil.
  Image data links have a :image-data-fun parameter.
  \(See `org-link-set-parameters'.)
  The value of the :image-data-fun parameter is a function
  taking the PROTOCOL, the LINK, and the DESCRIPTION as arguments.
  If that function returns nil the link is not interpreted as image.
  Otherwise the return value is the image data string to be displayed.

  Note that only bracket links are allowed as image data links
  with one of the formats [[PROTOCOL:LINK]] or [[PROTOCOL:LINK][DESCRIPTION]] are recognized."
  (interactive)
  (when (and (called-interactively-p 'any)
	     (use-region-p))
    (setq beg (region-beginning)
	  end (region-end)))
  (when (display-graphic-p)
    (org-with-wide-buffer
     (goto-char (or beg (point-min)))
     (when-let ((image-data-link-parameters
		 (cl-loop for link-par-entry in org-link-parameters
			  with fun
			  when (setq fun (plist-get (cdr link-par-entry) :image-data-fun))
			  collect (cons (car link-par-entry) fun)))
		(image-data-link-re (regexp-opt (mapcar 'car image-data-link-parameters)))
		(re (format "\\[\\[\\(%s\\):\\([^]]+\\)\\]\\(?:\\[\\([^]]+\\)\\]\\)?\\]"
			    image-data-link-re)))
       (while (re-search-forward re end t)
	 (let* ((protocol (match-string-no-properties 1))
		(link (match-string-no-properties 2))
		(description (match-string-no-properties 3))
		(image-data-link (assoc-string protocol image-data-link-parameters))
		(el (save-excursion (goto-char (match-beginning 1)) (org-element-context)))
		image-data)
	   (when el
	     (setq image-data
		   (or (let ((old (get-char-property-and-overlay
				   (org-element-property :begin el)
				   'org-image-overlay)))
			 (and old
			      (car-safe old)
			      (overlay-get (cdr old) 'display)))
		       (funcall (cdr image-data-link) protocol link description)))
	     (when image-data
	       (let ((ol (org-image-update-overlay image-data el t t)))
		 (when (and ol description)
		   (overlay-put ol 'after-string description)))))))))))

(advice-add #'org-display-inline-images :after #'org-display-user-inline-images)
  ;;; org-yt.el ends here
#+END_SRC

** Inline online images
Sometimes it's nice to display online images inline. Copied from [[https://emacs.stackexchange.com/questions/42281/org-mode-is-it-possible-to-display-online-images][this answer]].
Requires the youtube package above.

#+BEGIN_SRC emacs-lisp
(defun org-image-link (protocol link _description)
  "Interpret LINK as base64-encoded image data."
  (cl-assert (string-match "\\`img" protocol) nil
	     "Expected protocol type starting with img")
  (let ((buf (url-retrieve-synchronously (concat (substring protocol 3) ":" link))))
    (cl-assert buf nil
	       "Download of image \"%s\" failed." link)
    (with-current-buffer buf
      (goto-char (point-min))
      (re-search-forward "\r?\n\r?\n")
      (buffer-substring-no-properties (point) (point-max)))))

(org-link-set-parameters
 "imghttp"
 :image-data-fun #'org-image-link)

(org-link-set-parameters
 "imghttps"
 :image-data-fun #'org-image-link)
#+END_SRC

** Export
Org export extensions.

#+BEGIN_SRC emacs-lisp
;; Github markdown
(use-package ox-gfm)
;; Twitter bootstrap
(use-package ox-twbs)
;; Syntax highlighting in exported html
(use-package htmlize)
#+END_SRC

* Minor packages
#+BEGIN_SRC emacs-lisp
(use-package smartparens
  :demand
  :bind (("M-j" . sp-forward-barf-sexp)
	 ("M-ø" . sp-forward-slurp-sexp)
	 ("M-l" . sp-backward-bard-sexp)
	 ("M-k" . sp-backward-slurp-sexp))
  :config
  (require 'smartparens-config)
  (smartparens-global-mode)
  (show-smartparens-global-mode)
					; Automatic newline when pressing enter between parens
  (sp-local-pair 'c-mode "{" nil :post-handlers '(("||\n[i]" "RET")))
  (sp-local-pair 'c++-mode "{" nil :post-handlers '(("||\n[i]" "RET"))))

(use-package dumb-jump
  :bind (:map evil-normal-state-map
	      ("g d" . dumb-jump-go)
	      ("g b" . dumb-jump-back)
	      ("g i" . dumb-jump-quick-look))
  :config
  (dumb-jump-mode))
#+END_SRC

* Local packages
As local packages will vary between computers, the loading is not comitted to git.
Instead, it's loaded in another file that is ignored in git.

#+BEGIN_SRC emacs-lisp
(let ((local-packages (concat (file-name-directory (or load-file-name buffer-file-name)) "local_packages.el")))
  (when (file-exists-p local-packages)
    (load-file local-packages)))
#+END_SRC

* Custom functions
** Edit this config
#+BEGIN_SRC emacs-lisp
;; Get the filepath when the code is first executed.
;; The code is moved to a .el file of the same name
;; as this file when it is executed, so we must replace
;; the file extension to gen the right file
(setq config-file-path (replace-regexp-in-string
			"\\.el$"
			".org"
			(or load-file-name buffer-file-name)))

(defun edit-config ()
  "Open this config file in a new buffer."
  (interactive)
  (find-file config-file-path))
#+END_SRC

** Create org src block and open in new buffer
#+BEGIN_SRC emacs-lisp
(defun org-src-create-and-open (lang)
  "Create a src block for the language the user types in, and open it in a new buffer."
  (interactive
   (list (read-string "Programming language (emacs-lisp): ")))
  (when (equal lang "")
    (setq lang "emacs-lisp"))
  (insert (format "#+BEGIN_SRC %s\n\n#+END_SRC" lang))
  (previous-line)
  (org-edit-src-code))

(define-key org-mode-map (kbd "C-c b") 'org-src-create-and-open)
#+END_SRC

** Ert tests
Run tests from tests.el, or all tests in tests/, interactively.

#+BEGIN_SRC emacs-lisp
#+END_SRC

** Rename current file
#+BEGIN_SRC emacs-lisp
;; Originally from stevey, adapted to support moving to a new directory.
(defun rename-file-and-buffer (new-name)
  "Renames both current buffer and file it's visiting to NEW-NAME."
  (interactive
   (progn
     (if (not (buffer-file-name))
         (error "Buffer '%s' is not visiting a file!" (buffer-name)))
     ;; Disable ido auto merge since it too frequently jumps back to the original
     ;; file name if you pause while typing. Reenable with C-z C-z in the prompt.
     (let ((ido-auto-merge-work-directories-length -1))
       (list (read-file-name (format "Rename %s to: " (file-name-nondirectory
                                                       (buffer-file-name))))))))
  (if (equal new-name "")
      (error "Aborted rename"))
  (setq new-name (if (file-directory-p new-name)
                     (expand-file-name (file-name-nondirectory
                                        (buffer-file-name))
                                       new-name)
                   (expand-file-name new-name)))
  ;; Only rename if the file was saved before. Update the
  ;; buffer name and visited file in all cases.
  (if (file-exists-p (buffer-file-name))
      (rename-file (buffer-file-name) new-name 1))
  (let ((was-modified (buffer-modified-p)))
    ;; This also renames the buffer, and works with uniquify
    (set-visited-file-name new-name)
    (if was-modified
        (save-buffer)
      ;; Clear buffer-modified flag caused by set-visited-file-name
      (set-buffer-modified-p nil)))

  (setq default-directory (file-name-directory new-name))

  (message "Renamed to %s." new-name))
#+END_SRC
* Hooks
** Delete trailing whitespace when saving
#+BEGIN_SRC emacs-lisp
(add-hook 'before-save-hook 'delete-trailing-whitespace)
#+END_SRC

* Keybindings
Bindings that are spesific to a package are defined together
with that package.
Bindings spesific to the different
states for evil are defined under [[*Evil mode][Evil mode]].
Bindings to custom functions in this file are
defined together with those functions.

** Move between windows
#+BEGIN_SRC emacs-lisp
;(defvar my-win-change-minor-mode-map
;  (let ((map (make-sparse-keymap)))
;    (define-key map (kbd "C-j") 'windmove-left)
;    (define-key map (kbd "C-k") 'windmove-down)
;    (define-key map (kbd "C-l") 'windmove-up)
;    (define-key map (kbd "C-ø") 'windmove-right)
;    map)
;  "my-win-change-minor-mode keymap.")

(define-minor-mode my-win-change-minor-mode
  "A minor mode that adds keybindings for moving between windows."
  :lighter " win-change"
  :keymap (make-sparse-keymap))
  ;:init-value t

(evil-define-key 'normal 'my-win-change-minor-mode
  (kbd "C-j") 'windmove-left
  (kbd "C-k") 'windmove-down
  (kbd "C-l") 'windmove-up
  (kbd "C-ø") 'windmove-right)

(defun my-win-change-minibuffer-hook ()
  (my-win-change-minor-mode 0))

;(add-hook 'minibuffer-setup-hook 'my-win-change-minibuffer-hook)

;(my-win-change-minor-mode 1)
(add-hook 'text-mode-hook #'my-win-change-minor-mode)
#+END_SRC

** Cycle buffers
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-x k") 'next-buffer)
(global-set-key (kbd "C-x l") 'previous-buffer)
#+END_SRC

** Font size
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-+") 'text-scale-increase)
(global-set-key (kbd "C--") 'text-scale-decrease)
#+END_SRC
